plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group = 'org.ananurbaiti'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Web UI Testing
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.20.0'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.8.0'

    // BDD
    testImplementation 'io.cucumber:cucumber-java:7.14.0'
    testImplementation 'io.cucumber:cucumber-testng:7.14.0'
    testImplementation 'tech.grasshopper:extentreports-cucumber7-adapter:1.7.0'

    // API Testing
    testImplementation 'io.rest-assured:rest-assured:5.5.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'

    // Assertion
    testImplementation 'org.assertj:assertj-core:3.25.1'
    testImplementation 'org.hamcrest:hamcrest:3.0'

    // Dotenv
    implementation 'io.github.cdimascio:java-dotenv:5.2.2'

    // JSON Parsing
    testImplementation("com.jayway.jsonpath:json-path:2.9.0")
    implementation 'io.rest-assured:json-schema-validator:5.3.0'

    // Faker
    testImplementation("com.github.javafaker:javafaker:1.0.2")

    // Logging
    testImplementation 'org.slf4j:slf4j-simple:2.0.13'

    // TestNG
    testImplementation 'org.testng:testng:7.9.0'

    // âœ… Allure for TestNG & Cucumber
    testImplementation 'io.qameta.allure:allure-testng:2.25.0'
    testImplementation 'io.qameta.allure:allure-cucumber7-jvm:2.25.0'

}

test {
    useTestNG()
}

tasks.register('uiTest', Test) {
    useTestNG {
        includeGroups 'ui'
    }
    include '**/ui/runners/**'  // Only run BDD runners for web
    testLogging.showStandardStreams = true
    outputs.upToDateWhen { false }

    // Forward all -D properties to the test JVM
    systemProperties System.getProperties()

    reports {
        html.required.set(true)
        //junitXml.required.set(true)
    }

    // Optional: Print browser property to verify
    doFirst {
        println ">>> Running web tests with browser = ${System.getProperty("browser")}"
    }
}

tasks.register('apiTest', Test) {
    useTestNG {
        includeGroups 'api'
    }
    include '**/api/**/*.class'
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    outputs.upToDateWhen { false }
    reports {
        html.required.set(true)
    }
}

allure {
    version="2.25.0"
    autoconfigure(true)
    aspectjweaver(true)

    useTestNG {
        version="2.25.0"
    }
}

tasks.register('cleanAllureResults', Delete) {
    delete file('build/allure-results')
}